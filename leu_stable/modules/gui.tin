#nop --
#nop Class Definitions
#nop --

#var gui-description {Funzioni della GUI}
#var gui-help {
Gestione dell'interfaccia grafica.\n
Gli elementi sono della gui sono:\n
title_bar (la barra in alto)\n
bottom_bar (barra in basso)\n
side_bar (barra laterale destra)\n
mappa.\n
}

#nop --
#nop Modloader Stuff
#nop --

#alias gui-register {
	#nop need to check: events;
	#if {@isloaded{functions}} {
		register_module gui
	} {
		fail_module gui
	}
}

#NOP -------------------------------------------------------------------------;
#NOP struttura della gui;
#NOP -------------------------------------------------------------------------;
#VARIABLE {gui}
{
     {rows} {40}
     {cols} {111}
     {top} {7}
     {bot} {5}
     {left} {1}
     {right} {0}
}

#VARIABLE {title_bar}
{
     {toprow} {1}
     {topcol} {1}
}

#VARIABLE {bottom_bar}
{
     {toprow} {-5}
     {topcol} {1}
     {botrow} {-2}
     {botcol} {-1}
}


#NOP -------------------------------------------------------------------------;
#NOP Inizializza l'interfaccia grafica;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_init}
{
    #MAP create;
    map_start;
    #VAR hasLogged 1
    gui_refresh;
    title_bar_draw;
    load_alias_sidebar
}

#NOP -------------------------------------------------------------------------;
#NOP Ridimensiona l'interfaccia grafica
#NOP -------------------------------------------------------------------------;
#ALIAS {resize_gui}
{
    #if {&hasLogged != 0} 
    {
      #SCREEN get rows {gui[rows]};
      #SCREEN get cols {gui[cols]};

      #if {$gui[rows] > 30 && $gui[cols] > 110} {
        #SPLIT {$gui[top]} {$gui[bot]} {$gui[left]} -100 1;
        #MAP flag vtmap on;
        #MATH {gui[right]} {$gui[cols] - 100};
        #MAP offset 18 {-$gui[right]+3} -10 -3;
        #DRAW white calign box {17} {-$gui[right]+2} -7 -1 ;
        #MATH {side_bar[topcol]} {-$gui[right]+2};
        #SCREEN clear top;
        comm_bar_draw_data $comm_bar[active];
        #delay {0.5} {comm_bar_draw_tabs};
        side_bar_draw_data $side_bar[active];
        #delay {0.5} {side_bar_draw_tabs}
        };    
      #elseif {$gui[rows] > 30} {
        #MAP flag vtmap on;
        #SCREEN clear all;
        #SPLIT 13 {$gui[bot]} 0 0 1;
        #MAP offset 8 1 14 -1;
        #VAR {comm_bar[botrow]} 7;
        #delay {0.5} {comm_bar_draw_tabs};
        };
      #else {
        #MAP flag vtmap off;
        #SCREEN clear all; 
        #SPLIT 1 5 0 0 1;
        };
    }
}

#NOP -------------------------------------------------------------------------;
#NOP Aggiorna l'interfaccia grafica;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_refresh}
{
    #if {&hasLogged != 0} 
    {
      resize_gui;
      title_bar_draw;
      bottom_bar_draw;
      #BUFFER refresh;
    }
}

#NOP -------------------------------------------------------------------------;
#NOP Quando si riceve una linea dal mud aggiorna.;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_line_update}
{
    #if {&hasLogged != 0} 
    {
      bottom_bar_draw;
      #MAP update
    }
}

#nop -------------------------------------------------------------------------;
#nop Draw some buttons over the health bar.;
#nop -------------------------------------------------------------------------;
#ALIAS {title_bar_draw}
{
    #line ignore #showme {* $MSDP_CHARACTER_NAME} {1};
    #line ignore #showme {\e]68;2;TAB;help help\a\e[4;24mAiuto\e[24m} {1} {-19};
    #line ignore #showme {\e]68;2;TAB;show_modules\a\e[4;24mModuli\e[24m} {1} {-12};
    #line ignore #showme {\e]68;2;TAB;#end\a\e[4;24mEsci\e[24m} {1} {-4}
}

#nop -------------------------------------------------------------------------;
#nop Draw a health bar.;
#nop -------------------------------------------------------------------------;
#alias {bottom_bar_draw}
{
    #math {hp_percent} {100 * $MSDP_HEALTH / $MSDP_HEALTH_MAX};
    #math {hpbar1}   {$hp_percent / 10};
    #math {hpbar2}   {10 - $hpbar1};
    #math {mana_percent} {100 * $MSDP_MANA / $MSDP_MANA_MAX};
    #math {manabar1}   {$mana_percent / 10};
    #math {manabar2}   {10 - $manabar1};
    #math {mov_percent} {100 * $MSDP_MOVEMENT / $MSDP_MOVEMENT_MAX};
    #math {movbar1}   {$mov_percent / 10};
    #math {movbar2}   {10 - $movbar1};

    #FORMAT {hpbar} {<011>%+${hpbar1}s<099><000>%+${hpbar2}s<099>};
    #FORMAT {manabar} {<015>%+${manabar1}s<099><000>%+${manabar2}s<099>};
    #FORMAT {movbar} {<012>%+${movbar1}s<099><000>%+${movbar2}s<099>};

    #FORMAT prompt1 {  %s * hp:[%+5s/%-5s]} $hpbar $MSDP_HEALTH $MSDP_HEALTH_MAX;
    #FORMAT prompt2 {  %s * ma:[%+5s/%-5s]} $manabar $MSDP_MANA $MSDP_MANA_MAX;
    #FORMAT prompt3 {  %s * mv:[%+5s/%-5s]} $movbar $MSDP_MOVEMENT $MSDP_MOVEMENT_MAX;

    #if {&MSDP_HEALTH}
    {
      #LINE ignore #SHOWME {$prompt1 Lv : $MSDP_LEVEL Exp: $MSDP_EXPERIENCE Gld: $MSDP_MONEY]} {-5};
      #if {&MSDP_OPPONENT_NAME} 
      {
         #LINE ignore #SHOWME {$prompt2 Trg: $MSDP_OPPONENT_NAME Thp: $MSDP_OPPONENT_HEALTH Stato: $button_status_trg} {-4};
      };
      #else
      {
	     #LINE ignore #SHOWME {$prompt2 Trg: nessuno Stato: na} {-4};
      };
      #LINE ignore #SHOWME {$prompt3 Stato: $button_status_tnk Spl: $incanti} {-3};
    };
    #else 
    {
      #LINE ignore #SHOWME { ATTENDO I DATI } {$bottom_bar[botrow]-3};
      #LINE ignore #SHOWME {   DI MSDP } {$bottom_bar[botrow]-2};
      msdp_ask_bottombar
    };
    #DRAW white calign box {$bottom_bar[toprow]-1} {$bottom_bar[topcol]} {$bottom_bar[botrow]} {$bottom_bar[botcol]}
}

#ALIAS {button_status_tnk}
{
    #VARIABLE button_status_tnk %1
}

#ALIAS {button_status_trg}
{
    #VARIABLE button_status_trg %1
}

#NOP -------------------------------------------------------------------------;
#NOP Riga di comando stile zmud, mantine il comando digitato;
#NOP e lo evidenzia in blu. se dato;
#NOP -------------------------------------------------------------------------;

#CONFIG REPEAT_ENTER ON

#VARIABLE last_input {}

#EVENT {HISTORY UPDATE}
{
    #DELAY 0 #LINE ignore #SHOWME {<aaf><FFF><788>%0<088>} {-1};
    #VARIABLE last_input {%0}
}

#EVENT {PROCESSED KEYPRESS}
{
	#if {{%1} != {10} && {$last_input} !== {}}
	{
		#VARIABLE last_input {};
		#LINE ignore #SHOWME {} {-1};
		#cursor end;
		#history insert {}
	};
}

#NOP -------------------------------------------------------------------------;
#NOP Execute speedwalks with .;
#NOP -------------------------------------------------------------------------;

#alias {.%0}
{
	#var cnt {};

	#parse {%0} {char}
	{
		#if {"$char" >= "0" && "$char" <= "9"}
		{
			#var cnt $cnt$char
		};
		#elseif {"$cnt" == ""}
		{
			#send $char
		};
		#else
		{
			#$cnt #send $char;
			#var cnt {}
		};
	}
}

#ALIAS {small_button_bar}
{
    #DRAW green box -6 0 -2 -1 {};
    #DRAW green calign box -6 1 -4 5 {W};
    #BUTTON {-6 1 -4 5} {w};
    #DRAW green calign box -6 6 -4 10 {N};
    #BUTTON {-6 6 -4 10} {n};
    #DRAW green calign box -6 11 -4 15 {S};
    #BUTTON {-6 11 -4 15} {s};
    #DRAW green calign box -6 16 -4 20 {E};
    #BUTTON {-6 16 -4 20} {e};
    #DRAW green calign box -6 21 -4 25 {U};
    #BUTTON {-6 21 -4 25} {u};
    #DRAW green calign box -6 26 -4 30 {D};
    #BUTTON {-6 26 -4 30} {d};
    #DRAW green calign box -6 31 -4 35 {Trg};
    #BUTTON {-6 31 -4 35} {show_trig};
    #DRAW green calign box -6 36 -4 40 {Als};
    #BUTTON {-6 35 -4 40} {show_alias};
    #DRAW green calign box -6 41 -4 44 {bU};
    #BUTTON {-6 39 -4 44} {#buffer up};
    #DRAW green calign box -6 45 -4 48 {bD};
    #BUTTON {-6 43 -4 48} {#buffer down};
    button_status_tnk Standing;
}

#ALIAS {button_status %1}
{
    #DRAW white calign box -6 49 -4 59 {"%1"};
}