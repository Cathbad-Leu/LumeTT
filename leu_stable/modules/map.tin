#NOP VSOF;
#nop --
#nop Class Definitions
#nop --

#var map-description {Funzioni della Mappa}
#var map-help {
Gestione della mappa\n
Creazione e definizione della mappa\n
Alcune actions (triggers) legati a leu\n
Vedi #help Map\n
}
#var map-register-delay 3

#nop --
#nop Modloader Stuff
#nop --

#alias map-register {
	#if {!@isloaded{functions}} {
		#showme ERROR: functions module not loaded;
		fail_module map;
		#class map kill
	};
}

#nop -------------------------------------------------------------------------;
#nop Inizializza la mappa;
#nop -------------------------------------------------------------------------;

#ALIAS {map_start}
{
    #map create;
    #map flag nofollow;
    #map flag vtmap on;
    #map flag static;
    #map flag blockgraphics on;
    #map read ./leu_stable/saves/map.dat;
    #map goto 1;

    #map terrain {Forest} {<228>⟑} DENSE WIDE;
    #map terrain {Field} {<128>·} DENSE WIDE;
    #map terrain {Desert} {<cca>·} DENSE NARROW;
    #map terrain {Inside} {<f707880>▒} DENSE WIDE;
    #map terrain {Deep Water} {<f2232ff>▒} DENSE WIDE;
    #map terrain {Water} {<f2284ff>▒} DENSE WIDE;
}

#EVENT {MAP SHORT-CLICKED MOUSE BUTTON ONE}
{
    #map run {%0} 0.25
}

#nop -------------------------------------------------------------------------;
#nop Script per seguire un pergorso fino la sua fine es trave East.;
#nop -------------------------------------------------------------------------;

#VARIABLE {travel} {0}

#VARIABLE {revdir}
{
     {Down} {Up}
     {East} {West}
     {North} {South}
     {South} {North}
     {Up} {Down}
     {West} {East}
}
#VARIABLE {longdir}
{
     {d} {Down}
     {e} {East}
     {n} {North}
     {s} {South}
     {u} {Up}
     {w} {West}
}
#VARIABLE {shortdir}
{
     {Down} {d}
     {East} {e}
     {North} {n}
     {South} {s}
     {Up} {u}
     {West} {w}
}
#FUNCTION {finddir}
{
     #if {"%1" != "%2"}
     {
          #return %2
     };
     #else
     {
          #return %3
     };
}

#ALIAS {travel}
{
     #if {$travel}
     {
          #variable travel 0;
          #showme <118>Ti fermi dal tuo viaggio.
     };
     #else
     {
          #variable travel 1;
          #variable lastdir $revdir[$longdir[%0]];
          #showme <118>Viaggi a %0.;
          %0
     };
}

#ACTION {Uscite: %1}
{
     #if {$travel}
     {
          #if {"%1" != "%* %*"}
          {
               #variable travel 0;
               #showme <118>Ti fermi nel tuo viaggio per colpa di un vicolo cieco.
          };
          #else
          {
               #regexp {%1} {%* %* %*}
               {
                    #var travel 0;#showme <118>Ti fermi nel tuo viaggio per decidere una direzione.
               };
               #else
               {
                    #regexp {%1} {%* %*}
                    {
                         #var newdir @finddir{$lastdir;&1;&2};#showme <118>Viaggi verso $newdir.;#var lastdir $revdir[$newdir];$shortdir[$newdir]
                    }
               }
          };
     };
}
{1}
