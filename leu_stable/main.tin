#!tt++
#NOP VSOF;
#NOP -------------------------------------------------------------------------;
#NOP File principale, carica e predisponi tutto;
#NOP -------------------------------------------------------------------------;

#NOP -- Importa le librerie         --;
#var lib ./leu_stable/lib
#var mod ./leu_stable/modules
#var saves ./leu_stable/saves
#read $lib/colors.tin
#read $lib/filesystem.tin
#read $lib/modloader.tin

#NOP -- Carica i moduli di /modules --;
load_module functions
load_module syslog
load_module events
load_module help
load_module counter
load_module telnet
load_module msdp
load_module mxp
load_module mslp
load_module gmcp
load_module macro
load_module alias
load_module triggers
load_module map
load_module gui
load_module comm_bar;
load_module side_bar;

#VARIABLE host new-leu.leu.it
#VARIABLE port 6000

#NOP -------------------------------------------------------------------------;
#NOP PEr chi ha il tastierino numerico imposta il movimento con esso;
#NOP -------------------------------------------------------------------------;

#macro {\eOq} {d; #echo d}
#macro {\eOr} {s; #echo s}
#macro {\eOt} {w; #echo w}
#macro {\eOu} {look; exits; info mob; info obj}
#macro {\eOv} {e; #echo e}
#macro {\eOw} {u; #echo u}
#macro {\eOx} {n; #echo n}

#NOP -------------------------------------------------------------------------;
#NOP All'avvio chiede quale mud e personaggio usare;
#NOP -------------------------------------------------------------------------;

#EVENT {PROGRAM START}
{
    #NOP CONFIG {SCROLL_LOCK} {OFF};
    #NOP CONFIG {VERBOSE} {OFF};
    #NOP CONFIG {DEBUG TELNET} {OFF};
    #CONFIG {PACKET PATCH} {0.5};
    #CONFIG {MOUSE} {ON};
    #NOP CONFIG {MOUSE TRACKING} {OFF};
    #NOP CONFIG {MOUSE} {INFO};
    #NOP INFO {BUTTON} {OFF};
    #NOP INFO {EVENT} {OFF};

    #CLASS charsdata read $saves/muddata.dat;
    #if {&{charslist} == 0}
    {
        #class charsdata open;
        #list charslist clear;
        #class charsdata close;
    };

    #READ motd.tin;

    #ECHO {%h} {{ LUMEN ET UMBRA }};
    #ECHO { };
    #ECHO { CONNECT e il nome del personaggio};
    #ECHO { ADDPRG nome personaggio password};
    #ECHO { DELPRG nome del pg per cancellare};
    #ECHO  { %s per creare un nuovo personaggio} {\e]68;2;EXEC;nuovo\a\e[4mNUOVO\e[24m};
    #ECHO  { %s} {\e]68;2;EXEC;continua\a\e[4mCONTINUA\e[24m};
    #ECHO { };
    #ECHO {%h};
    connect
}

#ALIAS {continua}
{
    #SESSION leu $host $port;
}

#ALIAS {addprg}
{
    #if {&charslist[%1][char] == "%1"}
    {
        #LINE ignore #SHOWME {Personaggio già presente}
    };
    #elseif {"%1" == "" | "%2" == ""}
    {
        #LINE ignore #SHOWME {Sintassi: addprg <nome personaggio> <password>}
    };
    #else
    {
        #VARIABLE charslist[%1] %1;
        #VARIABLE charslist[%1][char] %1;
        #VARIABLE charslist[%1][pass] %2;
        #LINE ignore #SHOWME {Aggiunto: $charslist[%1][char]};
        #CLASS charsdata write $saves/muddata.dat
    };
    connect
}

#ALIAS {delprg}
{
    #if {&charslist[%1] != 0}
    {
        #UNVAR charslist[%1];
        #CLASS charsdata write $saves/muddata.dat;
        #LINE ignore #SHOWME Il mud %1 è stato cancellato.
    };
    #else
    {
        #ECHO {Il personaggio %s non è stato trovato nella lista.} {%1}
    };
    connect
}

#ALIAS {connect}
{
    #if {"%1" == ""}
    {
        #ECHO {%h} {{ LISTA PERSONAGGI }};
        #ECHO { };
        #foreach {*charslist[]} {chars}
        {
            #ECHO { %-10s %s} {$charslist[$chars][char]} {\e]68;2;EXEC;connect $chars\a\e[4mconnect\e[24m};
        };
        #ECHO { };
        #ECHO {%h};
    };
    #elseif {&charslist[%1] != 0}
    {
        #VARIABLE mud {%1};
        #SESSION leu $host $port;
    };
    #else
    {
        #ECHO {Non è stato possibile trovare %1.}
    };
}

#ALIAS {nuovo}
{
    #SESSION leu $host $port;
    #SEND {nuovo};
}

#NOP -------------------------------------------------------------------------;
#NOP Eventi di sessione;
#NOP -------------------------------------------------------------------------;

#EVENT {SESSION CONNECTED}
{
    #VARIABLE mud @gts{$mud};
    #if {&charslist[$mud]}
    {
        #if {&charslist[$mud][char]}
        {
            #SEND {$charslist[$mud][char]};
            #if {&charslist[$mud][pass]}
            {
                #SEND {$charslist[$mud][pass]};
            };
        };
    };
    gui_init;
    gui_refresh;
}

#EVENT {SESSION DISCONNECTED}
{
    #MAP write $saves/map.dat
}

#EVENT {SESSION DESTROYED}
{
    #gts #delay 0 #end
}

#NOP -------------------------------------------------------------------------;
#NOP EVENTI VARI;
#NOP -------------------------------------------------------------------------;

#EVENT {IAC WILL MSDP}
{
    #SEND {$TELNET[IAC]$TELNET[DO]$TELNET[MSDP]\};
    #delay 1 msdp_report
}

#EVENT {IAC SB MSDP}
{
    #VARIABLE {MSDP_%0} {%1}
}

#EVENT {IAC SB MSDP SCREEN_WIDTH}
{
    gui_refresh;
}

#EVENT {IAC SB MSDP SCREEN_HEIGHT}
{
    gui_refresh
}

#EVENT {IAC SB MSDP AC}
{
    gui_refresh
}

#EVENT {IAC SB NAWS}
{  
    gui_refresh
}

#EVENT {SCREEN RESIZE}
{
    gui_refresh;
    #SCREEN refresh;
    #BUFFER end
}

#EVENT {SCREEN ROTATE LANDSCAPE}
{
    gui_refresh
}

#EVENT {SCREEN ROTATE PORTRAIT}
{ 
    gui_refresh
}

#EVENT {RECEIVED LINE}
{
    gui_line_update
}

#EVENT {RECEIVED ERROR}
{
	#echo <118>%h { TOKENIZER };
	#info tokenizer -1;
	#echo <118>%h;
}

#EVENT {IAC WILL GMCP}
{
    #SEND {$TELNET[IAC]$TELNET[DO]$TELNET[GMCP]\};
}

#NOP -------------------------------------------------------------------------;
#NOP Use mouse click to change the input cursor's position.;
#NOP -------------------------------------------------------------------------;

#EVENT {SHORT-CLICKED MOUSE BUTTON ONE -1}
{
	#cursor position %1
}

#NOP -------------------------------------------------------------------------;
#NOP LOG on or off;
#NOP -------------------------------------------------------------------------;
#ALIAS {log}
{
     #if {"%0" == "on"}
     {
          #if {"$logging" != "on"}
          {
               #FORMAT {logfile} {logs/%t.html} {%Y-%m-%d};
               #log append $logfile;
               #VARIABLE logging on;
          };
          #else
          {
               #LINE ignore #SHOWME Stai già facendo un log.;
          }
     };
     #elseif {"%0" == "off"}
     {
          #if {"$logging" == "on"}
          {
               #log off;
               #VARIABLE logging off;
          };
          #else
          {
               #LINE ignore #SHOWME You are not logging.;
          };
     };
     #else
     {
          #LINE ignore #SHOWME Turn logging on or off?;
     };
}
